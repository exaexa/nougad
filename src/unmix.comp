#version 430

layout(local_size_x_id = 0) in;
layout(constant_id = 1) const uint k = 32; // these are dummy values for glslang
layout(constant_id = 2) const uint d = 32; // they must be rewritten by user
layout(constant_id = 3) const uint iters = 200;

layout(push_constant) uniform Params
{
  uint n;
  float alpha;
  float accel;
}
params;

layout(std430, binding = 0) buffer sbuf
{
  float s_kd[];
};
layout(std430, binding = 1) buffer spwbuf
{
  float spw_kd[];
};
layout(std430, binding = 2) buffer snwbuf
{
  float snw_kd[];
};
layout(std430, binding = 3) buffer nwbuf
{
  float nw_k[];
};
layout(std430, binding = 4) buffer ybuf
{
  float y_nd[];
};
layout(std430, binding = 5) buffer xbuf
{
  float x_nk[];
};
layout(std430, binding = 6) buffer rbuf
{
  float r_nd[];
};

void
main()
{
  uint ni = gl_GlobalInvocationID.x;
  float lastg_k[k], g_k[k];
  float tmp;
  if (ni >= params.n)
    return;

  for (uint ki = 0; ki < k; ++ki)
    lastg_k[ki] = 0;

  for (uint ii = 0; ii < iters; ++ii) {
    for (uint di = 0; di < d; ++di) {
      tmp = 0;
      for (uint ki = 0; ki < k; ++ki)
        tmp += x_nk[ni + params.n * ki] * s_kd[ki + k * di];
      r_nd[ni + params.n * di] = tmp - y_nd[ni + params.n * di];
    }

    for (uint ki = 0; ki < k; ++ki) {
      g_k[ki] = min(x_nk[ni + params.n * ki], 0) * nw_k[ki];
    }

    for (uint di = 0; di < d; ++di) {
      tmp = r_nd[ni + params.n * di];
      for (uint ki = 0; ki < k; ++ki) {
        float a = spw_kd[ki + k * di];
        float b = snw_kd[ki + k * di];
        g_k[ki] += tmp * (tmp > 0 ? a : b);
      }
    }

    for (uint ki = 0; ki < k; ++ki) {
      tmp = g_k[ki] * params.alpha;
      if (tmp * lastg_k[ki] > 0)
        tmp += params.accel * lastg_k[ki];
      x_nk[ni + params.n * ki] -= tmp;
      lastg_k[ki] = tmp;
    }
  }
}
